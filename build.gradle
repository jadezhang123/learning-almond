/* 插件 */
apply plugin: 'java'
apply from: 'dependencies.gradle'
/*全局配置*/
allprojects {
    /*坐标*/
    group 'own.jadezhang.learning'
    version '0.0.1-SNAPSHOT'
    /*自带插件*/
    apply plugin: 'java'
    apply plugin: 'maven'
    /*1.7环境*/
    sourceCompatibility = 1.7
    targetCompatibility = 1.7
    /*UTF-8*/
    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
        options.warnings = false
    }
}
/** 子项目配置 */
subprojects {
    /*类库配置*/
    repositories {
        mavenLocal()
        //阿里云maven仓库
        repositories { maven { url 'http://maven.aliyun.com/nexus/content/groups/public/' } }
        mavenCentral()
    }

    dependencies {

        compile(group: "own.jadezhang.learning.base", name: "base-common-domain", version: "$baseCommonVersion", changing: true)

        //加载libs目录下所有的jar
        compile fileTree(dir: 'libs', include: ['*.jar'])

        testCompile "junit:junit:$junitVersion"
        testCompile "org.springframework:spring-test:$springframworkVersion"
    }

    //生成源码jar
    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }
    //生成javadoc的jar
    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives jar
        archives sourcesJar
        archives javadocJar
    }

    //发布到Maven本地仓库
    uploadArchives {
        repositories {
            mavenDeployer {
                repository(url: "$localMavenRepository")
            }
        }
    }
}